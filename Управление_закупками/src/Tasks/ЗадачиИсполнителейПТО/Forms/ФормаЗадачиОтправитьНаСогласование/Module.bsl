#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВнутреннийЗаказ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БизнесПроцесс,"ВнутреннийЗаказ");

	Если   Не ВнутреннийЗаказСуществуетИНеУдален(ВнутреннийЗаказ) Тогда  
		
		Отказ = Истина;
		Возврат;		
		
	КонецЕсли;

	
	Если Объект.Выполнена Тогда
		ИнструкцияВыполненияЗадачи = "Задача выполнена.";
		ЭтаФорма.ТолькоПросмотр = Истина;
	Иначе
		ИнструкцияВыполненияЗадачи = "Для выполнения задачи необходимо:
										|1. Исправить документ внутреннего заказа 
										| 	согласно полученным замечаниям.
										|2. После исправления внутреннего заказа нажмите 
										|   кнопку - ""Отправить на согласование и закрыть.""";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте                                                
Процедура ОткрытьВнутреннийЗаказ(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", ВнутреннийЗаказ); 
	ОткрытьФорму("Документ.ВнутреннийЗаказ.Форма.ФормаДокумента",ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ОтправитьНаСогласование(Команда) 
	
	Если  Не ВнутреннийЗаказСуществуетИНеУдален(ВнутреннийЗаказ) Тогда 
		
		Возврат; 
		
	КонецЕсли;  
	
	НомерИДатаВнутреннегоЗаказа =  НомерИДатаВнутреннегоЗаказа(ВнутреннийЗаказ);
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Ждать ВопросАсинх(СтрШаблон("Вы действительно хотите отправить заказ %1 от %2 на согласование?",НомерИДатаВнутреннегоЗаказа.Номер,Формат(НомерИДатаВнутреннегоЗаказа.Дата,"ДФ=dd.MM.yyyy") ), Режим, 0);
	Если Ответ = КодВозвратаДиалога.Нет Тогда 
		
		ТекстСообщения = "Операция прервана пользователем.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);  
		
		Возврат;	
	КонецЕсли;
	
	
	ВыполнитьТекущуюЗадачуНаСервере();  
	
	Если Не Объект.Выполнена Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Оповестить("Запись_ЗадачиЗадачиИсполнителейПТО");  	
	
	ЭтаФорма.Модифицированность = Ложь;
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьТекущуюЗадачуНаСервере()

	НачатьТранзакцию(); 	
	
	Попытка 
		
       	ТекущаяЗадача = РеквизитФормыВЗначение("Объект"); 
		ТекущаяЗадача.Исполнитель = КадрыСервер.ТекущийСотрудник();
		ТекущаяЗадача.Подразделение = Справочники.Подразделения.ПустаяСсылка();
		ТекущаяЗадача.Должность = Справочники.Должности.ПустаяСсылка();


		ТекущаяЗадача.Записать();
		ТекущаяЗадача.ВыполнитьЗадачу();
		ЗначениеВРеквизитФормы(ТекущаяЗадача,"Объект");
		
		ЗафиксироватьТранзакцию();
		
	Исключение 
		
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
    	ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Операция не может быть выполнена по причине:'") + Символы.ПС + ТекстСообщения);	
		
		ОтменитьТранзакцию(); 
		Возврат;
		
	КонецПопытки; 
	
КонецПроцедуры      

&НаСервере
Функция ВнутреннийЗаказСуществуетИНеУдален(ВнутреннийЗаказ)
	
Если   Не ДокументыСервер.ВнутреннийЗаказСуществуетИНеУдален(ВнутреннийЗаказ) Тогда  
		
		НомерИДатаВнутреннегоЗаказа =  НомерИДатаВнутреннегоЗаказа(ВнутреннийЗаказ);

		ТекстСообщения = СтрШаблон("Согласование заказа %1 от %2 не возможно по причине его удаления из базы данных."
									, НомерИДатаВнутреннегоЗаказа.Номер, Формат(НомерИДатаВнутреннегоЗаказа.Дата,"ДФ=dd.MM.yyyy")); 
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения); 
		
		Возврат Ложь;
		
	Иначе            
		
		Возврат Истина;
		
	КонецЕсли;	     
	
КонецФункции    

&НаСервере
Функция НомерИДатаВнутреннегоЗаказа (ВнутреннийЗаказ)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВнутреннийЗаказ,"Номер, Дата");	
	
КонецФункции

#КонецОбласти
