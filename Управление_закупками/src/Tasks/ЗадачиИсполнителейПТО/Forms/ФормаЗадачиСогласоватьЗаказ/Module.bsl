#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВнутреннийЗаказ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БизнесПроцесс,"ВнутреннийЗаказ");
		
	Если   Не ВнутреннийЗаказСуществуетИНеУдален(ВнутреннийЗаказ) Тогда  
		
		Отказ = Истина;
		Возврат;		
		
	КонецЕсли;
	
	
	Если Объект.Выполнена Тогда
		ИнструкцияВыполненияЗадачи = "Задача выполнена.";
		ЭтаФорма.ТолькоПросмотр = Истина;
	Иначе
		ИнструкцияВыполненияЗадачи = "Для выполнения задачи необходимо:
										|1. Проверить документ внутреннего заказа.
										|2. Если все устраивает нажмите на кнопку 
										|	а) ""Согласовать и отправить в ОМТС"" (если необходимо заказ отправить сразу в ОМТС).
										|	б) ""Согласовать"" (заказ будет согласован, но НЕ будет отправлен в ОМТС.
										|4. Если имеются замечания, то:
										|	а) Назначьте исполнителя и сформулируйте замечания.
										|	б) Отправьте заказ на доработку нажав кнопку ""Отправить на доработку"".";
	КонецЕсли;
	
КонецПроцедуры   

#КонецОбласти   

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьВнутреннийЗаказ(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", ВнутреннийЗаказ); 
	ОткрытьФорму("Документ.ВнутреннийЗаказ.Форма.ФормаДокумента",ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОтправитьНаДоработку(Команда)   
	
	Если  Не ВнутреннийЗаказСуществуетИНеУдален(ВнутреннийЗаказ) Тогда 
 	
		Возврат; 
		
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.ИсполнительБудущейЗадачи) Тогда

		ТекстСообщения = "Ошибка. Не назначен исполнитель задачи.
							|Назначьте исполнителя задачи и повторите попытку.";
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Объект.ИсполнительБудущейЗадачи");
			
		Возврат;
		
	КонецЕсли;  
	
	НомерИДатаВнутреннегоЗаказа =  НомерИДатаВнутреннегоЗаказа(ВнутреннийЗаказ); 
		
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Ждать ВопросАсинх(СтрШаблон("Вы действительно хотите отправить заказ %1 от %2 на доработку?",НомерИДатаВнутреннегоЗаказа.Номер,Формат(НомерИДатаВнутреннегоЗаказа.Дата,"ДФ=dd.MM.yyyy") ), Режим, 0);
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда  
		
		ТекстСообщения = "Операция прервана пользователем.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);  
		
		Возврат; 
		
	КонецЕсли;  
	
		

	ВыполнитьТекущуюЗадачуНаСервере();  
	
	Если Не Объект.Выполнена Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Оповестить("Запись_ЗадачиЗадачиИсполнителейПТО");  	
	
	ЭтаФорма.Модифицированность = Ложь;
	ЭтаФорма.Закрыть();
	
КонецПроцедуры  	

&НаКлиенте
Асинх Процедура Согласовать(Команда)  
	
	Если Не ДокументыКлиент.ЗаказПроведен(ВнутреннийЗаказ) Тогда

		ТекстСообщения = "Не могу выполнить операцию.
						  |Внутренний заказ обязательно должен быть проведен.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);	
		Возврат; 
		
	КонецЕсли;  

	НомерИДатаВнутреннегоЗаказа =  НомерИДатаВнутреннегоЗаказа(ВнутреннийЗаказ); 
			
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Ждать ВопросАсинх(СтрШаблон("Вы действительно хотите согласовать заказ %1 от %2?",НомерИДатаВнутреннегоЗаказа.Номер,Формат(НомерИДатаВнутреннегоЗаказа.Дата,"ДФ=dd.MM.yyyy") ), Режим, 0);
	Если Ответ = КодВозвратаДиалога.Нет Тогда 
		
		ТекстСообщения = "Операция прервана пользователем.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
		
	КонецЕсли;
	
	Если  СтатусСогласованоУстановленУспешно(ВнутреннийЗаказ,Истина,Ложь) Тогда
		
		ВыполнитьТекущуюЗадачуНаСервере();			
		
	КонецЕсли;
	
	Если Не Объект.Выполнена Тогда
		
		ТекстСообщения = "Непредвиденная ошибка. Текущая задача не выполнена.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,);
		
		Возврат;
		
	КонецЕсли;  
	
	Ждать ПредупреждениеАсинх(СтрШаблон("Внутренний зааз %1 от %2 успешно согласован.",НомерИДатаВнутреннегоЗаказа.Номер,Формат(НомерИДатаВнутреннегоЗаказа.Дата,"ДФ=dd.MM.yyyy")),15,"Согласование внутреннего заказа");
	
	
	Оповестить("Запись_ЗадачиЗадачиИсполнителейПТО");  	
	
	ЭтаФорма.Модифицированность = Ложь;  
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СогласоватьИОтправитьВОМТС(Команда)
	
	Если  Не ВнутреннийЗаказСуществуетИНеУдален(ВнутреннийЗаказ) Тогда 
		
		Возврат; 
		
	КонецЕсли; 
	
	Если Не ДокументыКлиент.ЗаказПроведен(ВнутреннийЗаказ) Тогда
		
		ТекстСообщения = "Не могу выполнить операцию.
		|Внутренний заказ обязательно должен быть проведен.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);	
		Возврат; 
		
	КонецЕсли;   
	
    НомерИДатаВнутреннегоЗаказа =  НомерИДатаВнутреннегоЗаказа(ВнутреннийЗаказ);
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Ждать ВопросАсинх(СтрШаблон("Вы действительно хотите согласовать заказ %1 от %2 и отправить в ОМТС?",НомерИДатаВнутреннегоЗаказа.Номер,Формат(НомерИДатаВнутреннегоЗаказа.Дата,"ДФ=dd.MM.yyyy") ), Режим, 0);
	Если Ответ = КодВозвратаДиалога.Нет Тогда 
		
		ТекстСообщения = "Операция прервана пользователем.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;  
		
	КонецЕсли;  
	
	
	Если  СтатусСогласованоУстановленУспешно(ВнутреннийЗаказ,Истина,Истина) Тогда
		
		ВыполнитьТекущуюЗадачуНаСервере();			
		
	КонецЕсли;
	
	Если Не Объект.Выполнена Тогда
		
		ТекстСообщения = "Непредвиденная ошибка. Текущая задача не выполнена.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,);
		
		Возврат;
		
	КонецЕсли;  
	
	Ждать ПредупреждениеАсинх(СтрШаблон("Внутренний зааз %1 от %2 успешно согласован и отправлен в ОМТС.",НомерИДатаВнутреннегоЗаказа.Номер,Формат(НомерИДатаВнутреннегоЗаказа.Дата,"ДФ=dd.MM.yyyy")),15,"Согласование внутреннего заказа");
	
	Оповестить("Запись_ЗадачиЗадачиИсполнителейПТО");  	
	
	ЭтаФорма.Модифицированность = Ложь;  
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВнутреннийЗаказСуществуетИНеУдален(ВнутреннийЗаказ)
	
Если   Не ДокументыСервер.ВнутреннийЗаказСуществуетИНеУдален(ВнутреннийЗаказ) Тогда  
		
		НомерИДатаВнутреннегоЗаказа =  НомерИДатаВнутреннегоЗаказа(ВнутреннийЗаказ);

		ТекстСообщения = СтрШаблон("Согласование заказа %1 от %2 не возможно по причине его удаления из базы данных."
									, НомерИДатаВнутреннегоЗаказа.Номер, Формат(НомерИДатаВнутреннегоЗаказа.Дата,"ДФ=dd.MM.yyyy")); 
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения); 
		
		Возврат Ложь;
		
	Иначе            
		
		Возврат Истина;
		
	КонецЕсли;	     
	
КонецФункции

&НаСервере
Функция НомерИДатаВнутреннегоЗаказа (ВнутреннийЗаказ)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВнутреннийЗаказ,"Номер, Дата");	
	
КонецФункции

&НаСервере
Функция  СтатусСогласованоУстановленУспешно(ВнутреннийЗаказ,Согласован,ОтправитьВОМТС)

	Возврат Документы.ВнутреннийЗаказ.УстановитьРеквизитыПриСогласованииИОтправкиДокументаВОМТС(ВнутреннийЗаказ,Согласован,ОтправитьВОМТС);
 	
КонецФункции            

&НаСервере
Процедура ВыполнитьТекущуюЗадачуНаСервере()

	НачатьТранзакцию(); 	
	
	Попытка 
		
       	ТекущаяЗадача = РеквизитФормыВЗначение("Объект");
		ТекущаяЗадача.Исполнитель = КадрыСервер.ТекущийСотрудник();    
		ТекущаяЗадача.Подразделение = Справочники.Подразделения.ПустаяСсылка();
		ТекущаяЗадача.Должность = Справочники.Должности.ПустаяСсылка();

		ТекущаяЗадача.Записать();
		ТекущаяЗадача.ВыполнитьЗадачу();
		ЗначениеВРеквизитФормы(ТекущаяЗадача,"Объект");
		
		ЗафиксироватьТранзакцию();
		
	Исключение 
		
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
    	ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Операция не может быть выполнена по причине:'") + Символы.ПС + ТекстСообщения);	
		
		ОтменитьТранзакцию(); 
		Возврат;
		
	КонецПопытки; 
	
КонецПроцедуры 


#КонецОбласти

