#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	КонтрагентыСервер.ПриСозданииНаСервере(ЭтаФорма);  
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)

	ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиСписка", 0.2, Истина); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
 #КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы
#КонецОбласти

#Область ОбработчикиКомандФормы
#КонецОбласти

#Область СлужебныеПроцедурыИФункции    

&НаКлиенте
Процедура ОбработатьАктивизациюСтрокиСписка()      

	Если Не КонтрагентыКлиент.КонтрагентИзСпискаВыбранКорректно(ЭтаФорма,"Список") Тогда  
		ЗаполнитьПанельИнформацииПоДаннымКонтрагента(Неопределено)	
	Иначе    
		ЗаполнитьПанельИнформацииПоДаннымКонтрагента(Элементы.Список.ТекущаяСтрока);
	КонецЕсли; 

КонецПроцедуры  


&НаСервере
Процедура ЗаполнитьПанельИнформацииПоДаннымКонтрагента(Контрагент)

	ДанныеКонтрагента = Неопределено;
	Если Контрагент = Неопределено Тогда
		
		НаименованиеТекущегоКонтрагента			          = НСтр("ru = 'Контрагент не выбран'");
		ДоговорыТекущегоКонтрагента                       = НСтр("ru = 'Договоры'");
		КонтактныеЛицаТекущегоКонтрагента				  = НСтр("ru = 'Контактные лица'");
		
	Иначе    
		
		ДанныеКонтрагента = КонтрагентыСервер.ДанныеКонтрагентаДляПанелиИнформации(Контрагент);	
		
		НаименованиеТекущегоКонтрагента = ДанныеКонтрагента.Наименование;
		КонтактныеЛицаТекущегоКонтрагента = СтрШаблон("Контактные лица (%1)",ДанныеКонтрагента.КоличествоКонтактныхЛиц);
		ДоговорыТекущегоКонтрагента =  СтрШаблон("Договоры (%1)",ДанныеКонтрагента.КоличествоДоговоров);
		
	КонецЕсли;
	
	ЗаполнитьДанныеКИКонтрагента(ДанныеКонтрагента);
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаТекущегоКонтрагентаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не КонтрагентыКлиент.КонтрагентИзСпискаВыбранКорректно(ЭтаФорма,"Список") Тогда  
		
		ТекстСообщения = "Контрагент не выбран.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения); 
		Возврат;  
		
	КонецЕсли;  
	
	

	Контрагент = Элементы.Список.ТекущаяСтрока;
	
	ЗначениеОтбора = Новый Структура("Владелец", Контрагент );
	ПараметрыФормы = Новый Структура("Отбор", ЗначениеОтбора);

	
	ЗаголовокФормы = СтрШаблон("Контактные лица %1", Элементы.Список.ТекущиеДанные.Наименование);
	ПараметрыФормы.Вставить("Заголовок", ЗаголовокФормы); 
	
	ОткрытьФорму("Справочник.КонтактныеЛицаКонтрагентов.ФормаСписка",ПараметрыФормы,,Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыТекущегоКонтрагентаНажатие(Элемент, СтандартнаяОбработка) 
	
	СтандартнаяОбработка = Ложь;
	Если Не КонтрагентыКлиент.КонтрагентИзСпискаВыбранКорректно(ЭтаФорма,"Список") Тогда  
		
		ТекстСообщения = "Контрагент не выбран.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения); 
		Возврат;  
		
	КонецЕсли;
	
	Контрагент = Элементы.Список.ТекущаяСтрока;
	ЗначениеОтбора = Новый Структура("Владелец", Контрагент );
	ПараметрыФормы = Новый Структура("Отбор", ЗначениеОтбора);
		
	ЗаголовокФормы = СтрШаблон("Договоры заключенные с %1", Элементы.Список.ТекущиеДанные.Наименование);
	ПараметрыФормы.Вставить("Заголовок", ЗаголовокФормы); 
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаСписка",ПараметрыФормы,,Контрагент);

КонецПроцедуры

Процедура ЗаполнитьДанныеКИКонтрагента(ДанныеКонтрагента)
	
	Если ДанныеКонтрагента = Неопределено Тогда
		
		Для инд = 1 По КоличествоВидовКИ Цикл
			
			ЭтаФорма["ВидКИ_" + Строка(инд)] = НСтр("ru = '<Не указан>'");
			
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	Если ДанныеКонтрагента.КонтактнаяИнформацияТаблица.Количество() > КоличествоВидовКИ Тогда
		Возврат;
	КонецЕсли;
	
	Для инд = 1 По КоличествоВидовКИ Цикл
		
		Если ПустаяСтрока(ДанныеКонтрагента.КонтактнаяИнформацияТаблица[инд - 1].ПредставлениеКИ) Тогда
			ЭтаФорма["ВидКИ_" + Строка(инд)] = НСтр("ru = '<Не указан>'");
		Иначе
			ЭтаФорма["ВидКИ_" + Строка(инд)] = ДанныеКонтрагента.КонтактнаяИнформацияТаблица[инд - 1].ПредставлениеКИ;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СправочникКонтрагенты" 
		ИЛИ ИмяСобытия = "Запись_КонтактныеЛицаКонтрагентов"  
		ИЛИ ИмяСобытия = "Удаление_СправочникКонтактныеЛицаКонтрагентов"
		ИЛИ ИмяСобытия = "Запись_ДоговорыКонтрагентов" 
		ИЛИ ИмяСобытия = "Удаление_ДоговорыКонтрагентов" Тогда 
		
		ОбработатьАктивизациюСтрокиСписка();
		
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

