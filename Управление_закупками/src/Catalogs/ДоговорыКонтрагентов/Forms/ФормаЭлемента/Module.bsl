#Область ОбработчикиСобытийФормы         

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьЗаголовок();
	
	// СтандартныеПодсистемы.РаботаСФайлами  Создание кнопки и гиперсылки ПрисоединенныеФайлы
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект,ПараметрыГиперссылки); 
	// Конец СтандартныеПодсистемы.РаботаСФайлами 
	
КонецПроцедуры 

&НаСервере
Процедура  ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Проверить наличие совпадающей записи в справочнике	
	Если  СправочникиСервер.ИмеетсяДвойникВСправочнике(ТекущийОбъект,"Наименование, ДатаПодписания, Владелец")  Тогда 	
		ТекстСообщения = НСтр("ru = 'Отказ в записи элемента.
		|Номер договора с этим контрагентом уже существует в информационной базе.
		|Необходимо указать другую информацию.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.Номер",,Отказ);		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайлами.ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи, Параметры);
	// Конец СтандартныеПодсистемы.РаботаСФайлами   

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьЗаголовок();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы      

&НаКлиенте 

Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
	
	// СтандартныеПодсистемы.РаботаСФайлами. Вызвать процедуру при нажатии добавленной на форму кнопки, . 
	РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
	// Конец СтандартныеПодсистемы.РаботаСФайлами 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции     

&НаСервере
Процедура УстановитьЗаголовок()

	Если Не Параметры.Ключ.Пустая() Тогда
		ЭтаФорма.АвтоЗаголовок = Ложь;                             
		НаименованиеКонтагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "Наименование");
		Заголовок = СтрШаблон("Договор №%1 %2",Объект.Наименование,НаименованиеКонтагента);  
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти



