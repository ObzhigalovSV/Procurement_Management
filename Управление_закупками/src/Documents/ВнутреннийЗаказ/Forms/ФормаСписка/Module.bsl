#Область ОбработчикиСобытийФормы     

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьКоммандыОтправитьНаСогласованияВЗависимостиОтПрав(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если 	ИмяСобытия = "Запись_РегистрСведенийРаспределениеВыполняемыхРаботПоПодрядчикам"
		ИЛИ ИмяСобытия = "Запись_ЗадачиЗадачиИсполнителейПТО" Тогда 
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы   

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент) 
	
	ПодключитьОбработчикОжидания("УстановитьДоступностьКоммандыОтправитьНаСогласования", 0.2, Истина); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы     

&НаКлиенте
Асинх Процедура ОтправитьНаСогласование(Команда)
	
	Если Элементы.Список.ТекущаяСтрока = Неопределено Тогда
		
		ТекстСообщения = "Непредвиденная ошибка. Не определен номер внутреннего заказа";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
		
	КонецЕсли;
	
	Если Не ДокументыКлиент.ЗаказПроведен(Элементы.Список.ТекущаяСтрока) Тогда
		
		ТекстСообщения = "Не могу выполнить операцию.
		|Внутренний заказ обязательно должен быть проведен.";
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);	
		Возврат;
		
		
	КонецЕсли;    
	
	Режим = РежимДиалогаВопрос.ДаНет; 
	ТекущийЗаказ = Элементы.Список.ТекущиеДанные;
	
	Ответ = Ждать ВопросАсинх(СтрШаблон("Вы действительно хотите отправить заказ %1 от %2 на согласование?",ТекущийЗаказ.Номер,Формат(ТекущийЗаказ.Дата, "ДФ=dd.MM.yyyy")), Режим, 0);
	Если Ответ = КодВозвратаДиалога.Нет Тогда 

		ТекстСообщения = "Операция прервана пользователем.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
		
	КонецЕсли;

	Если БизнесПроцессыИЗадачиКлиент.СоздатьБПредачаЗаказаВОМТС(Элементы.Список.ТекущаяСтрока) = Неопределено Тогда
		
	 	ТекстСообщения = "Не могу запустить процесс согласования внутреннего заказа.
							|Обратитесь к Администратору 1С.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Оповестить("Запись_ЗадачиЗадачиИсполнителейПТО");
	Элементы.ФормаОтправитьНаСогласование.Доступность = Ложь;
	
	НомерВнутреннегоЗаказа = Элементы.Список.ТекущиеДанные.Номер;
	ДатаВнутреннегоЗаказа = Элементы.Список.ТекущиеДанные.Дата;
	
	Ждать ПредупреждениеАсинх(СтрШаблон("Внутренний зааз %1 от %2 успешно отправлен на согласование.",НомерВнутреннегоЗаказа,Формат(ДатаВнутреннегоЗаказа,"ДФ=dd.MM.yyyy")),15,"Согласование внутреннего заказа");



КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции   

&НаСервере
Процедура УстановитьВидимостьКоммандыОтправитьНаСогласованияВЗависимостиОтПрав() 
	
	Если Пользователи.РолиДоступны("ОтправитьВнутреннийЗаказНаСогласование") Тогда 
		Элементы.ФормаОтправитьНаСогласование.Видимость = Истина;
	Иначе	
		Элементы.ФормаОтправитьНаСогласование.Видимость = Ложь;	
	КонецЕсли;
	
КонецПроцедуры                                              


&НаКлиенте
Процедура УстановитьДоступностьКоммандыОтправитьНаСогласования()
	
	Если Элементы.Список.ТекущаяСтрока = Неопределено 
		Или  БизнесПроцессыИЗадачиКлиент.БППередачаЗаказаВОМТСУжеСуществует(Элементы.Список.ТекущаяСтрока) Тогда
		
		Элементы.ФормаОтправитьНаСогласование.Доступность = Ложь;
		
	Иначе  
		
		Элементы.ФормаОтправитьНаСогласование.Доступность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры    


#КонецОбласти




  
  