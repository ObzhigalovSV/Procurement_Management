
#Область ОбработчикиСобытийФормы    

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ИнициализироватьКомпановщикНастроекСКД();
	ОбновитьТаблицуПотребности();  
	
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		
		Элементы.ТоварыЗагрузитьИзФайла.Доступность = Ложь;		
		Элементы.ДобавитьОстатокВСвободныйЗапас.Доступность = Ложь;	
		
	КонецЕсли;
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	Если Объект.Ссылка = ПредопределенноеЗначение("Документ.ПриходнаяНакладная.ПустаяСсылка") И ЗначениеЗаполнено(Объект.Основание) Тогда
		
		ДокументыКлиент.ПересчитатьТабличнуюЧастьТовары(Объект.Товары, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС);		
		
	КонецЕсли; 
	
	УстановитьЗаголовкиТабличнойЧастиТовары();  
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
    Объект.СуммаДокумента = Объект.Товары.Итог("СуммаСНДС");
	Объект.СписокВнутреннихЗаказов = ПолучитьСписокВнутреннихЗаказов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры
	
#КонецОбласти                                                                                

#Область ОбработчикиСобытийЭлементовШапкиФормы  

&НаКлиенте
Процедура ЦенаВключаетНДСПриИзменении(Элемент)
	
	УстановитьЗаголовкиТабличнойЧастиТовары(); 
	ДокументыКлиент.ПересчитатьТабличнуюЧастьТовары(Объект.Товары,Объект.СтавкаНДС, Объект.ЦенаВключаетНДС)
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	
	Для Каждого ТекущиеДанные Из Объект.Товары Цикл  
		
		ДокументыКлиент.ПересчитатьСуммуНДС( ТекущиеДанные, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС); 
		ДокументыКлиент.ПересчитатьСуммуТовараСНДС(ТекущиеДанные, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти    

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗакрытыеПотребности   

&НаКлиенте
Процедура ЗакрытыеПотребностиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	ЗакрытьПотребностьТекущихПозиций(ПараметрыПеретаскивания.Значение);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПотребностьТекущихПозиций(МассивЭлементов)
	
		
	ТекущиеДанныеТаблицыТоварыПоНакладной = Элементы.ТоварыПоНакладной.ТекущиеДанные;
	
	Если ТекущиеДанныеТаблицыТоварыПоНакладной = Неопределено Тогда 
		
		ТекстСообщения = "Нет данных из табличной части Товары по накладной";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ТоварыПоНакладной");
		Возврат;
		
	КонецЕсли;
	
	Для Каждого ДанныеДляДобавления Из МассивЭлементов Цикл	
		
		ДобавитьДанныеВТЧЗакрытыеПотребности(ДанныеДляДобавления, ТекущиеДанныеТаблицыТоварыПоНакладной);
		
	КонецЦикла;

КонецПроцедуры   

&НаКлиенте
Процедура ЗакрытыеПотребностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные =  Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанныеСтроки =  Элемент.ТекущиеДанные;
	Если ТекущиеДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	
	Если Поле.Имя = "ЗакрытыеПотребностиПредставлениеПересчетаЕдиницИзмерения" Тогда
		
		Если ТекущиеДанныеСтроки.ЕдиницаИзмеренияПотребности = ТекущиеДанныеСтроки.ЕдиницаИзмеренияПоставщика Тогда
		
		ТекстСообщения = "Единицы измерения номенклатуры в приходной накладной и заказе сопадают.
							|Коэффициент пересчета единиц измерений,в данном случае, пересчитывать не нужно: Он всегда равен 1."; 
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Объект.ЗакрытыеПотребности["+ Элементы.ЗакрытыеПотребности.ТекущаяСтрока + "].ПредставлениеПересчетаЕдиницИзмерения"); 
		Возврат;	
		
	КонецЕсли;

		ПересчитатьЗакрытуюПотребностьСНовымКоэффициентом(ТекущиеДанные);
		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытыеПотребностиКоличествоЗакрытоеПоПриходнойНакладнойПриИзменении(Элемент)
	
	ТекущиеДанныеСтроки = Элементы.ЗакрытыеПотребности.ТекущиеДанные;
	Если ТекущиеДанныеСтроки = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной < 0 Тогда
		
		ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной = КоличествоЗакрытоеПоПриходнойНакладнойПередРедактированием;
		ТекстСообщения = "Ошибка. Отмена редактирования.
							|Количество не может принимать отрицательных значений."; 
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Объект.ЗакрытыеПотребности[" + Элементы.ЗакрытыеПотребности.ТекущаяСтрока + "].КоличествоЗакрытоеПоНакладной");
		Возврат;
		
	КонецЕсли; 
	
	Если ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной = КоличествоЗакрытоеПоПриходнойНакладнойПередРедактированием Тогда
		Возврат; //Данные не изменились. Пересчитывать не надо.
	КонецЕсли;
		
	ТекущиеДанныеТаблицыПотребность =   НайтиЭлементТаблицыПотребность(ТекущиеДанныеСтроки);   
	
	Если ТекущиеДанныеТаблицыПотребность = Неопределено Тогда 
		
		ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной = КоличествоЗакрытоеПоПриходнойНакладнойПередРедактированием;
		Возврат; //Непредвиденная ошибка. Вернуть все в исходное состояние.
		
	КонецЕсли;
	
	
	Если Не ЗначениеЗаполнено(ТекущиеДанныеСтроки.БазоваяЕдиницаИзмерения) Тогда 
		
		ТекстСообщения = "Откорректируйте паритет единиц измерений, а затем повторите ввод.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной = 0;
		ПересчитатьЗакрытуюПотребностьСНовымКоэффициентом(ТекущиеДанныеСтроки); 
		Возврат;
		
	КонецЕсли;
	
	ДоступноеКоличествоКРаспределению = ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности + ТекущиеДанныеТаблицыПотребность.Количество;
	КРаспределению = ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной * ТекущиеДанныеСтроки.КоэффициентЕдиницыИзмерения; //КоличествоЗакрытоеПотребности = Базовая ед. изм.
	Если ТекущиеДанныеСтроки.ЕдиницаИзмеренияПоставщика = ТекущиеДанныеСтроки.БазоваяЕдиницаИзмерения Тогда
		КРаспределению = ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной / ТекущиеДанныеСтроки.КоэффициентЕдиницыИзмерения; 
	КонецЕсли;
	
	Если ДоступноеКоличествоКРаспределению < КРаспределению И Не ТекущиеДанныеСтроки.РазрешитьСверхобъем Тогда
		
		ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности = ДоступноеКоличествоКРаспределению;
		ТекущиеДанныеТаблицыПотребность.Количество = 0;
		
	Иначе
		
		ТекущиеДанныеТаблицыПотребность.Количество = ТекущиеДанныеТаблицыПотребность.Количество - ( КРаспределению - ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности);
		ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности = КРаспределению;
		
	КонецЕсли;
	ПересчитатьЗакрытуюПотребностьСУчетомТекущегоКоэффициента(ТекущиеДанныеСтроки);
	КоличествоЗакрытоеПоПриходнойНакладнойПередРедактированием = ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной;
	КоличествоЗакрытоеПотребностиПередРедактированием = ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытыеПотребностиРазрешитьСверхобъемПриИзменении(Элемент)
	
	ТекущиеДанныеСтроки = Элементы.ЗакрытыеПотребности.ТекущиеДанные;
	Если ТекущиеДанныеСтроки = Неопределено Тогда 
		Возврат;
	КонецЕсли;   
	
	Если Не ТекущиеДанныеСтроки.РазрешитьСверхобъем Тогда 
		
		ТекущиеДанныеТаблицыПотребность =   НайтиЭлементТаблицыПотребность(ТекущиеДанныеСтроки);   
		
		Если ТекущиеДанныеТаблицыПотребность = Неопределено Тогда 
			
			ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности = КоличествоЗакрытоеПотребностиПередРедактированием;
			Возврат; //Непредвиденная ошибка. Вернуть все в исходное состояние.
			
		КонецЕсли;
		Если ТекущиеДанныеТаблицыПотребность.Количество < 0 Тогда
			ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности = ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности + ТекущиеДанныеТаблицыПотребность.Количество;
			ТекущиеДанныеТаблицыПотребность.Количество = 0;
			ПересчитатьЗакрытуюПотребностьСУчетомТекущегоКоэффициента(ТекущиеДанныеСтроки);
			КоличествоЗакрытоеПоПриходнойНакладнойПередРедактированием = ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной;
			КоличествоЗакрытоеПотребностиПередРедактированием = ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытыеПотребностиКоличествоЗакрытоеПотребностиПриИзменении(Элемент)
	
	ТекущиеДанныеСтроки = Элементы.ЗакрытыеПотребности.ТекущиеДанные;
	Если ТекущиеДанныеСтроки = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности < 0 Тогда
		
		ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности = КоличествоЗакрытоеПотребностиПередРедактированием;
		ТекстСообщения = "Ошибка. Отмена редактирования.
							|Количество не может принимать отрицательных значений."; 
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Объект.ЗакрытыеПотребности[" + Элементы.ЗакрытыеПотребности.ТекущаяСтрока + "].КоличествоЗакрытоеПотребности");
		Возврат;
		
	КонецЕсли; 
	
	Если ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности = КоличествоЗакрытоеПотребностиПередРедактированием Тогда
		Возврат; //Данные не изменились. Пересчитывать не надо.
	КонецЕсли;

	
	ТекущиеДанныеТаблицыПотребность =   НайтиЭлементТаблицыПотребность(ТекущиеДанныеСтроки);   
	
	Если ТекущиеДанныеТаблицыПотребность = Неопределено Тогда 
		
		ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности = КоличествоЗакрытоеПотребностиПередРедактированием;
		Возврат; //Непредвиденная ошибка. Вернуть все в исходное состояние.
		
	КонецЕсли;
	
	
	ТекущиеДанныеТаблицыПотребность.Количество =  ТекущиеДанныеТаблицыПотребность.Количество - (ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности - КоличествоЗакрытоеПотребностиПередРедактированием); 
	
	Если ТекущиеДанныеТаблицыПотребность.Количество < 0 И Не ТекущиеДанныеСтроки.РазрешитьСверхобъем   Тогда
		
		ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности = ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности + ТекущиеДанныеТаблицыПотребность.Количество;
		ТекущиеДанныеТаблицыПотребность.Количество = 0;
	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанныеСтроки.БазоваяЕдиницаИзмерения) Тогда 
		
		ТекстСообщения = "Откорректируйте паритет единиц измерений, а затем повторите ввод.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной = 0;
		ПересчитатьЗакрытуюПотребностьСНовымКоэффициентом(ТекущиеДанныеСтроки); 
		Возврат;
		
	КонецЕсли;
	
			
	ПересчитатьЗакрытуюПотребностьСУчетомТекущегоКоэффициента(ТекущиеДанныеСтроки);
	КоличествоЗакрытоеПоПриходнойНакладнойПередРедактированием = ТекущиеДанныеСтроки.КоличествоЗакрытоеПоНакладной;
	КоличествоЗакрытоеПотребностиПередРедактированием = ТекущиеДанныеСтроки.КоличествоЗакрытоеПотребности;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытыеПотребностиПриАктивизацииСтроки(Элемент)
	
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда  
		КоличествоЗакрытоеПотребностиПередРедактированием = 0;
		Возврат;
	КонецЕсли;
	
	КоличествоЗакрытоеПотребностиПередРедактированием = ТекущиеДанные.КоличествоЗакрытоеПотребности;
	КоличествоЗакрытоеПоПриходнойНакладнойПередРедактированием = ТекущиеДанные.КоличествоЗакрытоеПоНакладной;
	
	НайденныйЭлемент = 	 НайтиЭлементТаблицыПотребность(ТекущиеДанные);
	Если НайденныйЭлемент <> Неопределено Тогда 
			
		Элементы.Потребность.ТекущаяСтрока = НайденныйЭлемент.ПолучитьИдентификатор();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары       

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ДокументыКлиент.ПересчитатьСуммуНДС( ТекущиеДанные, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС); 
	ДокументыКлиент.ПересчитатьСуммуТовараСНДС(ТекущиеДанные, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС);

КонецПроцедуры
	
&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ДокументыКлиент.ПересчитатьСуммуНДС( ТекущиеДанные, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС); 
	ДокументыКлиент.ПересчитатьСуммуТовараСНДС(ТекущиеДанные, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные; 
	ДокументыКлиент.ПересчитатьЦенуТовараОтносительноСуммыНДС(ТекущиеДанные, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС);
	
	//Изменилась цена. Заново прочитать Текущие данные. Пересчитать Сумму Товара с НДС.	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ДокументыКлиент.ПересчитатьСуммуТовараСНДС(ТекущиеДанные, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС);	
	
КонецПроцедуры      

&НаКлиенте
Процедура ТоварыСуммаСНДСПриИзменении(Элемент) 
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные; 
	ДокументыКлиент.ПересчитатьЦенуТовараОтносительноСуммыТовараСНДС(ТекущиеДанные, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС);
	
	//Изменилась цена. Заново прочитать Текущие данные. Пересчитать Сумму НДС .	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ДокументыКлиент.ПересчитатьСуммуНДС(ТекущиеДанные, Объект.СтавкаНДС, Объект.ЦенаВключаетНДС);

КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)    
	
	
	Если ТекущаяСтраница = Элементы.СтраницаЗакрытиеПотребностей Тогда 
		
		ЗагрузитьТоварыПоПриходнойНакладной ();
		ИнициализироватьТаблицуТоварыПоПриходнойНакладной();

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	 Для Каждого ЭлементКУдалению ИЗ Элементы.Товары.ВыделенныеСтроки Цикл
		
		 ТекущиеДанныеУдаляемойСтроки = Элементы.Товары.ДанныеСтроки(ЭлементКУдалению);		
		 Отбор = Новый Структура("НоменклатураПоставщика, ЕдиницаИзмеренияПоставщика",
		 ТекущиеДанныеУдаляемойСтроки.НоменклатураПоставщика, ТекущиеДанныеУдаляемойСтроки.ЕдиницаИзмерения);
		 НайденныеСтроки = Объект.ЗакрытыеПотребности.НайтиСтроки(Отбор);
		 
		 Если  НайденныеСтроки.Количество() <> 0 Тогда
			 
			 Для Каждого УдаляемаяСтрока Из НайденныеСтроки Цикл
				 Объект.ЗакрытыеПотребности.Удалить(УдаляемаяСтрока);
			 КонецЦикла;
			 
			 
			 //Контроль удаления подчиненых записей 
			 Если Объект.ЗакрытыеПотребности.НайтиСтроки(Отбор).Количество() <> 0 Тогда
				 
				 ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка при удалении распределяемых позиций.",,,Отказ); 
				 
			 КонецЕсли;
			 
		 КонецЕсли;
		 
		 
		 
	 КонецЦикла;	
	
	
	
КонецПроцедуры



#КонецОбласти   

#Область ОбработчикиСобытийЭлементовТаблицыФормыТоварыПоНакладной

&НаКлиенте
Процедура ТоварыПоНакладнойПриАктивизацииСтроки(Элемент)
	
	//Установить отбор таблицы ЗакрытыеПотребности     		
	ТекущиеДанныеСтроки = Элементы.ТоварыПоНакладной.ТекущиеДанные;
	Если ТекущиеДанныеСтроки = Неопределено Тогда		
			Возврат;		
	КонецЕсли; 
	
	Элементы.ЗакрытыеПотребности.ОтборСтрок = Новый ФиксированнаяСтруктура("НоменклатураПоставщика, ЕдиницаИзмеренияПоставщика",ТекущиеДанныеСтроки.НоменклатураПоставщика, ТекущиеДанныеСтроки.ЕдиницаИзмерения );

	ОбновитьДанныеИЗаголовокОбОстаткеРаспределения();

КонецПроцедуры  

#КонецОбласти   

#Область ОбработчикиСобытийЭлементовТаблицыФормыКомпановщикНастроекОтбор       

&НаКлиенте
Процедура СписокНастройкиОтборПриИзменении(Элемент)
	
	ОбновитьТаблицуПотребности();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "ПриходнаяНакладная.Товары";
	ПараметрыЗагрузки.Заголовок = НСтр("ru='Загрузка списка товаров из файла'");   
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьПриходнуюНакладнуюИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки,Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФильтр(Команда)
	
	ОбновитьТаблицуПотребности();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОстатокВСвободныйЗапас(Команда)  
	
	ТекущиеДанныеСтрокиТаблицыТовары = Элементы.ТоварыПоНакладной.ТекущиеДанные;
	
	Если ТекущиеДанныеСтрокиТаблицыТовары = Неопределено Тогда
		
		ТекстСообщения = "Нет данных для распределения.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ТоварыПоНакладной");
		Возврат;
		
	КонецЕсли;	
	
	Если  КоличествоРаспределенноеКТекущейПозицииПоПриходнойНакладной(Элементы.ЗакрытыеПотребности.ОтборСтрок) = ТекущиеДанныеСтрокиТаблицыТовары.Количество Тогда
		ТекстСообщения = "Для текущей позиции по приходной накладной все количество уже распределено.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат; 
		
	КонецЕсли;
		
	Остаток =  ТекущиеДанныеСтрокиТаблицыТовары.Количество - КоличествоРаспределенноеКТекущейПозицииПоПриходнойНакладной(Элементы.ЗакрытыеПотребности.ОтборСтрок);
	НоваяСтрока  = Объект.ЗакрытыеПотребности.Добавить();
	НоваяСтрока.Номенклатура = ТекущиеДанныеСтрокиТаблицыТовары.НоменклатураПоставщика;               			 
	НоваяСтрока.ВнутреннийЗаказ = ПредопределенноеЗначение("Документ.ВнутреннийЗаказ.ПустаяСсылка");                         
	НоваяСтрока.ЕдиницаИзмеренияПотребности = ТекущиеДанныеСтрокиТаблицыТовары.ЕдиницаИзмерения;             		
	НоваяСтрока.ДополнительныеХарактеристикиНоменклатуры = ПредопределенноеЗначение("Справочник.ДополнительныеХарактеристикиНоменклатуры.ПустаяСсылка");
	НоваяСтрока.КоличествоЗакрытоеПотребности = Остаток;
		
	НоваяСтрока.НоменклатураПоставщика = ТекущиеДанныеСтрокиТаблицыТовары.НоменклатураПоставщика; 
    НоваяСтрока.ЕдиницаИзмеренияПоставщика =  ТекущиеДанныеСтрокиТаблицыТовары.ЕдиницаИзмерения;  
	НоваяСтрока.КоличествоЗакрытоеПоНакладной = Остаток;
	НоваяСтрока.РазрешитьСверхобъем = Истина;
	
	НоваяСтрока.БазоваяЕдиницаИзмерения = ТекущиеДанныеСтрокиТаблицыТовары.ЕдиницаИзмерения;
	НоваяСтрока.КоэффициентЕдиницыИзмерения = 1;
    УстановитьПредставлениеПересчетаЕдиницИзмерения(НоваяСтрока);
    ОбновитьТаблицуПотребности(); 
	ОбновитьДанныеИЗаголовокОбОстаткеРаспределения();
			
КонецПроцедуры

&НаКлиенте
Процедура ОткрепитьПотребность(Команда)
	  	
	ОткрепитьПотребностьТекущихПозиций();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПотребность(Команда)
	
	МассивЭлементов = Новый Массив;
	
	Для Каждого ВыбраннаяСтрока Из Элементы.Потребность.ВыделенныеСтроки Цикл
		
		МассивЭлементов.Добавить(Элементы.Потребность.ДанныеСтроки(ВыбраннаяСтрока));
		
	КонецЦикла;  
	
	ЗакрытьПотребностьТекущихПозиций(МассивЭлементов);
	
КонецПроцедуры

 #КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Процедура ИнициализироватьКомпановщикНастроекСКД()
		
  	МакетСКД = Документы.ПриходнаяНакладная.ПолучитьМакет("МакетСКД");
	Если ЗначениеЗаполнено(Объект.Основание) Тогда 
		МакетСКД = Документы.ПриходнаяНакладная.ПолучитьМакет("МакетСКДДляСчета");
	КонецЕсли;
	
	АдресСхемы = ПоместитьВоВременноеХранилище(МакетСКД,УникальныйИдентификатор);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы);
	
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(МакетСКД.НастройкиПоУмолчанию);

КонецПроцедуры  

&НаКлиенте
Процедура УстановитьЗаголовкиТабличнойЧастиТовары() 
	
	Если Объект.ЦенаВключаетНДС Тогда
		Элементы.Товары.ПодчиненныеЭлементы.ТоварыЦена.Заголовок = "Цена с НДС";
	Иначе
		Элементы.Товары.ПодчиненныеЭлементы.ТоварыЦена.Заголовок = "Цена без НДС";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуПотребности();  
		
	МакетСКД = Документы.ПриходнаяНакладная.ПолучитьМакет("МакетСКД");
	Если ЗначениеЗаполнено(Объект.Основание) Тогда 
		МакетСКД = Документы.ПриходнаяНакладная.ПолучитьМакет("МакетСКДДляСчета");
	КонецЕсли;

	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ПриходнаяНакладная", Объект.Ссылка);	
	
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("СчетКонтрагента", Объект.Основание);	
	КонецЕсли;
	
	ТЗЗакрытыеПотребности = Объект.ЗакрытыеПотребности.Выгрузить();

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(МакетСКД,Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	ВнешнийНаборДанных = Новый Структура("ТЗЗакрытыеПотребности", ТЗЗакрытыеПотребности);   
			
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,ВнешнийНаборДанных);
	
	Результат = Новый ТаблицаЗначений;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(Результат);  
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Результат.Свернуть("ВнутреннийЗаказ, Номенклатура, ДополнительныеХарактеристикиНоменклатуры, ЕдиницаИзмерения","Количество");
	Потребность.Очистить();	
	Потребность.Загрузить(Результат);

КонецПроцедуры	

&НаКлиенте
Процедура ЗагрузитьПриходнуюНакладнуюИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнителныеПараметры)Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьПриходнуюНакладнуюИзФайлаНаСервере(АдресЗагруженныхДанных);
	ДокументыКлиент.ПересчитатьТабличнуюЧастьТовары(Объект.Товары, Объект.СтавкаНДС,Объект.ЦенаВключаетНДС);

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПриходнуюНакладнуюИзФайлаНаСервере(АдресЗагружунныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагружунныхДанных);
	Для Каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл 
		
		   ЗаполнитьЗначенияСвойств(Объект.Товары.Добавить(), СтрокаТаблицы);
			
	КонецЦикла;
	
КонецПроцедуры 

&НаСервере
Процедура ЗагрузитьТоварыПоПриходнойНакладной () 
	
	ТоварыВыгрузка  = Объект.Товары.Выгрузить();
	ТоварыВыгрузка.Свернуть("НоменклатураПоставщика,ЕдиницаИзмерения ", "Количество" );
	ТоварыПоНакладной.Загрузить(ТоварыВыгрузка);

КонецПроцедуры

&НаКлиенте
Функция КоличествоРаспределенноеКТекущейПозицииПоПриходнойНакладной(ОтборФиксированнаяСтруктура);
	//ОтборФиксированнаяСтруктура - значение отбора строк в таблице Элементы.ЗакрытыеПотребности, содержащащей текущие данные из реквизита ТоварыПоПриходнойНакладной
	//Может принимать значение Неопределено. В этом случае в таблице Элементы.ЗакрытыеПотребности отбор не установлен.
	
	КоличествоКРаспределению = 0; 
	
	НайденныеЭлементы = Объект.ЗакрытыеПотребности.НайтиСтроки(Новый Структура(ОтборФиксированнаяСтруктура));
	
	Для Каждого ТекущаяСтрока Из НайденныеЭлементы Цикл
		
		КоличествоКРаспределению = КоличествоКРаспределению + ТекущаяСтрока.КоличествоЗакрытоеПоНакладной;
		
	КонецЦикла;
	
	Возврат КоличествоКРаспределению;	
	
КонецФункции

&НаКлиенте
Функция НайтиЭлементСДаннымиДляЗаписи(ТекущиеДанныеТаблицыТоварыПоНакладной, ТекущиеДанныеТаблицыПотребность)
// Поиск существующего элемента для будующей корректировки полей количества.	

	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", ТекущиеДанныеТаблицыПотребность.Номенклатура);
	Отбор.Вставить("ВнутреннийЗаказ",ТекущиеДанныеТаблицыПотребность.ВнутреннийЗаказ);
    Отбор.Вставить("ДополнительныеХарактеристикиНоменклатуры",ТекущиеДанныеТаблицыПотребность.ДополнительныеХарактеристикиНоменклатуры);
    Отбор.Вставить("ЕдиницаИзмеренияПотребности",ТекущиеДанныеТаблицыПотребность.ЕдиницаИзмерения);
    Отбор.Вставить("ЕдиницаИзмеренияПоставщика",ТекущиеДанныеТаблицыТоварыПоНакладной.ЕдиницаИзмерения);
    Отбор.Вставить("НоменклатураПоставщика",ТекущиеДанныеТаблицыТоварыПоНакладной.НоменклатураПоставщика);
	
	НайденныеЭлементы = Объект.ЗакрытыеПотребности.НайтиСтроки(Отбор);
	
	Если  НайденныеЭлементы.Количество() = 0 Тогда
		Возврат Неопределено; 
	Иначе
		Возврат НайденныеЭлементы[0];
	КонецЕсли;
	
КонецФункции   

&НаКлиенте
Функция НайтиЭлементТаблицыТоварыПоПриходнойНакладной(ТекущиеДанныеТаблицыЗакрытыеПотребности)
	

	Отбор = Новый Структура;
	Отбор.Вставить("НоменклатураПоставщика", ТекущиеДанныеТаблицыЗакрытыеПотребности.НоменклатураПоставщика);
	Отбор.Вставить("ЕдиницаИзмерения",ТекущиеДанныеТаблицыЗакрытыеПотребности.ЕдиницаИзмеренияПоставщика);
	
	НайденныеЭлементы = ТоварыПоНакладной.НайтиСтроки(Отбор);
	
	Если  НайденныеЭлементы.Количество() = 0 Тогда
		Возврат Неопределено; 
	Иначе
		Возврат НайденныеЭлементы[0];
	КонецЕсли;
	
КонецФункции    

&НаКлиенте
Функция НайтиЭлементТаблицыПотребность(ТекущиеДанныеТаблицыЗакрытыеПотребности)

	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", ТекущиеДанныеТаблицыЗакрытыеПотребности.Номенклатура);
	Отбор.Вставить("ВнутреннийЗаказ",ТекущиеДанныеТаблицыЗакрытыеПотребности.ВнутреннийЗаказ);
    Отбор.Вставить("ДополнительныеХарактеристикиНоменклатуры",ТекущиеДанныеТаблицыЗакрытыеПотребности.ДополнительныеХарактеристикиНоменклатуры);
    Отбор.Вставить("ЕдиницаИзмерения",ТекущиеДанныеТаблицыЗакрытыеПотребности.ЕдиницаИзмеренияПотребности);
   	
	НайденныеЭлементы = Потребность.НайтиСтроки(Отбор);
	
	Если  НайденныеЭлементы.Количество() = 0 Тогда
			
		СнятьФильтр();
		
		КоличествоЗакрытойПотребности = ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности;
		ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности = КоличествоЗакрытоеПотребностиПередРедактированием;
		ОбновитьТаблицуПотребности(); 
		ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности = КоличествоЗакрытойПотребности;
		НайденныеЭлементы = Потребность.НайтиСтроки(Отбор);

		Если НайденныеЭлементы.Количество() = 0 Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Непредвиденная ошибка. Обратитесь к Админстратору");
			Возврат Неопределено; 
		КонецЕсли;
				
	КонецЕсли;
	
	Возврат НайденныеЭлементы[0];

КонецФункции   

&НаКлиенте
Процедура ДобавитьДанныеВТЧЗакрытыеПотребности(ТекущиеДанныеТаблицыПотребность, ТекущиеДанныеТаблицыТоварыПоНакладной)
	
	Если (ТекущиеДанныеТаблицыПотребность.Количество <= 0) ИЛИ (Не ТипЗнч(ТекущиеДанныеТаблицыПотребность) = Тип("ДанныеФормыЭлементКоллекции")) Тогда 
		ТекстСообщения = "Нет данных для переноса";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Потребность");
		Возврат;
	КонецЕсли; 	 
	
	Если Элементы.ЗакрытыеПотребности.ОтборСтрок = Неопределено ИЛИ КоличествоРаспределенноеКТекущейПозицииПоПриходнойНакладной(Элементы.ЗакрытыеПотребности.ОтборСтрок) >= ТекущиеДанныеТаблицыТоварыПоНакладной.Количество Тогда

		ТекстСообщения = "Все количество для текущей позиции по приходной накладной уже распределено.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ТоварыПоНакладной[" + Элементы.ТоварыПоНакладной.ТекущаяСтрока + "].Количество");
		Возврат;
		
	КонецЕсли;
	
			
	ЭлементТаблицыЗакрытыеПотребности = НайтиЭлементСДаннымиДляЗаписи(ТекущиеДанныеТаблицыТоварыПоНакладной, ТекущиеДанныеТаблицыПотребность);
	
	
	Если  ЭлементТаблицыЗакрытыеПотребности = Неопределено Тогда
		
		ДобавитьНовыйЭлементВТаблицуЗакрытыеПотребности(ТекущиеДанныеТаблицыТоварыПоНакладной, ТекущиеДанныеТаблицыПотребность);
		
	Иначе
		
  		// Посчитать колчество с учетом границ 
		ЭлементТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности = ЭлементТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности
																		+ ТекущиеДанныеТаблицыПотребность.Количество;
        ТекущиеДанныеТаблицыПотребность.Количество = 0;
		
		УстановитьКакТекущуюСтрокуВТаблицеЗакрытыеПотребности(ЭлементТаблицыЗакрытыеПотребности);
		Если НЕ (ЗначениеЗаполнено(ЭлементТаблицыЗакрытыеПотребности.БазоваяЕдиницаИзмерения)
			И ЗначениеЗаполнено(ЭлементТаблицыЗакрытыеПотребности.КоэффициентЕдиницыИзмерения)) Тогда 
			ПересчитатьЗакрытуюПотребностьСНовымКоэффициентом(ЭлементТаблицыЗакрытыеПотребности);
		Иначе 
			
			ЭлементТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПоНакладной = 0;
			ПересчитатьЗакрытуюПотребностьСУчетомТекущегоКоэффициента(ЭлементТаблицыЗакрытыеПотребности);
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры
  
&НаКлиенте
Процедура ДобавитьНовыйЭлементВТаблицуЗакрытыеПотребности(ТекущиеДанныеТаблицыТоварыПоНакладной, ТекущиеДанныеТаблицыПотребность)
	
	Если ТекущиеДанныеТаблицыПотребность.Количество <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока  = Объект.ЗакрытыеПотребности.Добавить();
	НоваяСтрока.Номенклатура =  ТекущиеДанныеТаблицыПотребность.Номенклатура;               			 
	НоваяСтрока.ВнутреннийЗаказ = ТекущиеДанныеТаблицыПотребность.ВнутреннийЗаказ;                         
	НоваяСтрока.ЕдиницаИзмеренияПотребности = ТекущиеДанныеТаблицыПотребность.ЕдиницаИзмерения;             		
	НоваяСтрока.ДополнительныеХарактеристикиНоменклатуры = ТекущиеДанныеТаблицыПотребность.ДополнительныеХарактеристикиНоменклатуры;
	НоваяСтрока.КоличествоЗакрытоеПотребности = ТекущиеДанныеТаблицыПотребность.Количество;
	
	НоваяСтрока.НоменклатураПоставщика = ТекущиеДанныеТаблицыТоварыПоНакладной.НоменклатураПоставщика; 
	НоваяСтрока.ЕдиницаИзмеренияПоставщика =  ТекущиеДанныеТаблицыТоварыПоНакладной.ЕдиницаИзмерения; 
	
	НоваяСтрока.БазоваяЕдиницаИзмерения = Неопределено;
	НоваяСтрока.КоэффициентЕдиницыИзмерения = 0;
	НоваяСтрока.ПредставлениеПересчетаЕдиницИзмерения = "Неопределно";
	
	ТекущиеДанныеТаблицыПотребность.Количество = 0;
	
	УстановитьКакТекущуюСтрокуВТаблицеЗакрытыеПотребности(НоваяСтрока);
	
	Если НоваяСтрока.ЕдиницаИзмеренияПотребности = НоваяСтрока.ЕдиницаИзмеренияПоставщика Тогда 
		
		НоваяСтрока.БазоваяЕдиницаИзмерения = НоваяСтрока.ЕдиницаИзмеренияПотребности;
		НоваяСтрока.КоэффициентЕдиницыИзмерения = 1;
		УстановитьПредставлениеПересчетаЕдиницИзмерения(НоваяСтрока);
		ПересчитатьЗакрытуюПотребностьСУчетомТекущегоКоэффициента(НоваяСтрока);
		
	Иначе 
		
		УстановитьКакТекущуюСтрокуВТаблицеЗакрытыеПотребности(НоваяСтрока); 
		//ОпределитьбазовцюЕдининцуИПересчитатьЗакрытуюПотребность
		ПересчитатьЗакрытуюПотребностьСНовымКоэффициентом(НоваяСтрока);
		
	КонецЕсли;
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКакТекущуюСтрокуВТаблицеЗакрытыеПотребности(Элемент)
	
	Перем ОтборСтрок;
	
	ОтборСтрок = Элементы.ЗакрытыеПотребности.ОтборСтрок;  
	Элементы.ЗакрытыеПотребности.ОтборСтрок = Неопределено;
	Элементы.ЗакрытыеПотребности.ТекущаяСтрока = Элемент.ПолучитьИдентификатор();
	Элементы.ЗакрытыеПотребности.ОтборСтрок = ОтборСтрок;
	
КонецПроцедуры    

&НаКлиенте
Процедура УстановитьПредставлениеПересчетаЕдиницИзмерения(ТекущиеДанныеТаблициЗакрытыеПотребности)
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанныеТаблициЗакрытыеПотребности.БазоваяЕдиницаИзмерения)  ИЛИ НЕ ЗначениеЗаполнено(ТекущиеДанныеТаблициЗакрытыеПотребности.КоэффициентЕдиницыИзмерения) Тогда 
		ТекущиеДанныеТаблициЗакрытыеПотребности.ПредставлениеПересчетаЕдиницИзмерения = "Неопределено";		
		Возврат;
	КонецЕсли;
	
	Если  ТекущиеДанныеТаблициЗакрытыеПотребности.БазоваяЕдиницаИзмерения = ТекущиеДанныеТаблициЗакрытыеПотребности.ЕдиницаИзмеренияПотребности Тогда
		
		ТекущиеДанныеТаблициЗакрытыеПотребности.ПредставлениеПересчетаЕдиницИзмерения 
							= СтрШаблон("1 %1 = %2 %3", 
							ТекущиеДанныеТаблициЗакрытыеПотребности.ЕдиницаИзмеренияПоставщика,
							ТекущиеДанныеТаблициЗакрытыеПотребности.КоэффициентЕдиницыИзмерения,
							ТекущиеДанныеТаблициЗакрытыеПотребности.ЕдиницаИзмеренияПотребности );
			
	Иначе  
		
		ТекущиеДанныеТаблициЗакрытыеПотребности.ПредставлениеПересчетаЕдиницИзмерения 
							= СтрШаблон("1 %1 = %2 %3", 
							ТекущиеДанныеТаблициЗакрытыеПотребности.ЕдиницаИзмеренияПотребности,
							ТекущиеДанныеТаблициЗакрытыеПотребности.КоэффициентЕдиницыИзмерения,
							ТекущиеДанныеТаблициЗакрытыеПотребности.ЕдиницаИзмеренияПоставщика );
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьЗакрытуюПотребностьСНовымКоэффициентом(РедактируемыеДанныеТаблицыЗакрытыеПотребности)
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("ОбработатьПолученныеЗначения",ЭтаФорма,РедактируемыеДанныеТаблицыЗакрытыеПотребности);	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЕдиницаИзмеренияПоставщика",РедактируемыеДанныеТаблицыЗакрытыеПотребности.ЕдиницаИзмеренияПоставщика);
	ПараметрыФормы.Вставить("ЕдиницаИзмеренияПотребности",РедактируемыеДанныеТаблицыЗакрытыеПотребности.ЕдиницаИзмеренияПотребности);
	ПараметрыФормы.Вставить("НоменклатураПоставщика",РедактируемыеДанныеТаблицыЗакрытыеПотребности.НоменклатураПоставщика);
	ПараметрыФормы.Вставить("НоменклатураПотребности",РедактируемыеДанныеТаблицыЗакрытыеПотребности.Номенклатура); 
	ПараметрыФормы.Вставить("БазоваяЕдиницаИзмерения",РедактируемыеДанныеТаблицыЗакрытыеПотребности.БазоваяЕдиницаИзмерения);
	ПараметрыФормы.Вставить("КоэффициентЕдиницыИзмерения",РедактируемыеДанныеТаблицыЗакрытыеПотребности.КоэффициентЕдиницыИзмерения);	
	ОткрытьФорму("ОбщаяФорма.ФормаУстановкиКоэффициентаПересчета",ПараметрыФормы,Элементы.ЗакрытыеПотребности,,,,ОповещениеОВыборе,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолученныеЗначения (РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Если РезультатЗакрытия = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущиеДанныеТаблицыЗакрытыеПотребности = ДополнительныеПараметры;
	ТекущиеДанныеТаблицыЗакрытыеПотребности.БазоваяЕдиницаИзмерения = РезультатЗакрытия.БазоваяЕдиницаИзмерения;
	ТекущиеДанныеТаблицыЗакрытыеПотребности.КоэффициентЕдиницыИзмерения = РезультатЗакрытия.КоэффициентЕдиницыИзмерения;
	УстановитьПредставлениеПересчетаЕдиницИзмерения(ТекущиеДанныеТаблицыЗакрытыеПотребности); 
 
	ПересчитатьЗакрытуюПотребностьСУчетомТекущегоКоэффициента(ТекущиеДанныеТаблицыЗакрытыеПотребности);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФильтр()

	Для Каждого СтрокаОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		
		СтрокаОтбора.Использование = Ложь;	
		
	КонецЦикла;                           
	

КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьЗакрытуюПотребностьСУчетомТекущегоКоэффициента(ТекущиеДанныеТаблицыЗакрытыеПотребности)
		
	ТекущиеДанныеТаблицыТоварыПоНакладной = НайтиЭлементТаблицыТоварыПоПриходнойНакладной(ТекущиеДанныеТаблицыЗакрытыеПотребности);
	Если ТекущиеДанныеТаблицыТоварыПоНакладной = Неопределено Тогда 
		ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПоНакладной = КоличествоЗакрытоеПоПриходнойНакладнойПередРедактированием;
		ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка. Не найдены данные в таблице потребности",,"ТоварыПоНакладной");
		Возврат;
	КонецЕсли; 
	
	ТекущиеДанныеТаблицыПотребность =   НайтиЭлементТаблицыПотребность(ТекущиеДанныеТаблицыЗакрытыеПотребности);
	Если ТекущиеДанныеТаблицыПотребность = Неопределено Тогда 
	
			Возврат; 
				
	КонецЕсли; 
	
	
	ДопустимоеКоличествоПоНакладнойКРаспределению = ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПоНакладной 
												+ ТекущиеДанныеТаблицыТоварыПоНакладной.Количество
												- КоличествоРаспределенноеКТекущейПозицииПоПриходнойНакладной(Элементы.ЗакрытыеПотребности.ОтборСтрок); 
	
	Если ТекущиеДанныеТаблицыЗакрытыеПотребности.БазоваяЕдиницаИзмерения = ТекущиеДанныеТаблицыЗакрытыеПотребности.ЕдиницаИзмеренияПотребности Тогда
		
		КРаспределениюПоНакладной = ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности/ТекущиеДанныеТаблицыЗакрытыеПотребности.КоэффициентЕдиницыИзмерения;	
		
	Иначе
		
		КРаспределениюПоНакладной = ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности * ТекущиеДанныеТаблицыЗакрытыеПотребности.КоэффициентЕдиницыИзмерения;
		
	КонецЕсли;
	
	Если ДопустимоеКоличествоПоНакладнойКРаспределению < КРаспределениюПоНакладной Тогда
		
		ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПоНакладной = ДопустимоеКоличествоПоНакладнойКРаспределению;
		
		Если ТекущиеДанныеТаблицыЗакрытыеПотребности.БазоваяЕдиницаИзмерения = ТекущиеДанныеТаблицыЗакрытыеПотребности.ЕдиницаИзмеренияПотребности Тогда
			
			КоличествоКЗаписи = ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПоНакладной * ТекущиеДанныеТаблицыЗакрытыеПотребности.КоэффициентЕдиницыИзмерения;	
			
			Разница =  ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности - КоличествоКЗаписи; 			
			
			ТекущиеДанныеТаблицыПотребность.Количество = ТекущиеДанныеТаблицыПотребность.Количество + Разница; 
			
			ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности = КоличествоКЗаписи;
			
		Иначе
			
			КоличествоКЗаписи = ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПоНакладной / ТекущиеДанныеТаблицыЗакрытыеПотребности.КоэффициентЕдиницыИзмерения;	
			
			Разница =  ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности - КоличествоКЗаписи; 			
			
			ТекущиеДанныеТаблицыПотребность.Количество = ТекущиеДанныеТаблицыПотребность.Количество + Разница; 
			
			ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности = КоличествоКЗаписи;
			
		КонецЕсли;
		
	Иначе
		
		ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПоНакладной = КРаспределениюПоНакладной;	
		
	КонецЕсли;   
	
	ОбновитьДанныеИЗаголовокОбОстаткеРаспределения();  
	КоличествоЗакрытоеПоПриходнойНакладнойПередРедактированием =  ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПоНакладной;
	КоличествоЗакрытоеПотребностиПередРедактированием = ТекущиеДанныеТаблицыЗакрытыеПотребности.КоличествоЗакрытоеПотребности;
			
КонецПроцедуры
  
&НаКлиенте
Процедура ОбновитьДанныеИЗаголовокОбОстаткеРаспределения()

	ТекущиеДанныеСтрокиТаблицыТовары = Элементы.ТоварыПоНакладной.ТекущиеДанные;
	
	Если ТекущиеДанныеСтрокиТаблицыТовары = Неопределено Тогда
		
		Элементы.ГруппаПотребностьПоВнутреннимЗаказам.Заголовок = "Потребности по внутренним заказам";
		Возврат;
		
	КонецЕсли;
	
	ОсталосьРаспределить = ТекущиеДанныеСтрокиТаблицыТовары.Количество - КоличествоРаспределенноеКТекущейПозицииПоПриходнойНакладной(Элементы.ЗакрытыеПотребности.ОтборСтрок);
		
	ТекстЗаголовка = СтрШаблон("%1  - осталось распределить: %2 %3",ТекущиеДанныеСтрокиТаблицыТовары.НоменклатураПоставщика, 
	
	ОсталосьРаспределить , ТекущиеДанныеСтрокиТаблицыТовары.ЕдиницаИзмерения);
	
	Элементы.ГруппаПотребностьПоВнутреннимЗаказам.Заголовок = ТекстЗаголовка;  
	
	ТекущиеДанныеСтрокиТаблицыТовары.ОсталосьРаспределить = ОсталосьРаспределить;

  КонецПроцедуры

&НаКлиенте
Процедура ОткрепитьПотребностьТекущихПозиций()
	
	МассивСтрок = Новый Массив;
	Для Каждого ВыбраннаяСтрока Из Элементы.ЗакрытыеПотребности.ВыделенныеСтроки Цикл
		
		МассивСтрок.Добавить(Элементы.ЗакрытыеПотребности.ДанныеСтроки(ВыбраннаяСтрока));	
		               
	КонецЦикла;
	
	Для Каждого  ТекущаяСтрока из  МассивСтрок Цикл	
			
		Объект.ЗакрытыеПотребности.Удалить(ТекущаяСтрока); 
		
	КонецЦикла;
	
	ОбновитьТаблицуПотребности();
	ОбновитьДанныеИЗаголовокОбОстаткеРаспределения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПотребностьПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	ОткрепитьПотребностьТекущихПозиций();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьТаблицуТоварыПоПриходнойНакладной()
	
	Для Каждого ТекущиеДанныеСтроки Из ТоварыПоНакладной Цикл
		
		КоличествоКРаспределению = 0; 
		
		Отбор = Новый Структура("НоменклатураПоставщика, ЕдиницаИзмеренияПоставщика", ТекущиеДанныеСтроки.НоменклатураПоставщика,ТекущиеДанныеСтроки.ЕдиницаИзмерения);  
		
		НайденныеЭлементы = Объект.ЗакрытыеПотребности.НайтиСтроки(Отбор);
		
		Для Каждого ТекущаяСтрокаЗакрытыеПотребности Из НайденныеЭлементы Цикл
			
			КоличествоКРаспределению = КоличествоКРаспределению + ТекущаяСтрокаЗакрытыеПотребности.КоличествоЗакрытоеПоНакладной;
			
		КонецЦикла;
		
			
		ТекущиеДанныеСтроки.ОсталосьРаспределить = ТекущиеДанныеСтроки.Количество - КоличествоКРаспределению;	
		
		
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокВнутреннихЗаказов()
	
	МассивВнутреннихЗаказов = ОбщегоНазначения.ВыгрузитьКолонку(Объект.ЗакрытыеПотребности,"ВнутреннийЗаказ",Истина);
	СписокВнутреннихЗаказов = Объект.ЗакрытыеПотребности.Выгрузить(,"ВнутреннийЗаказ"); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫРАЗИТЬ(СписокВнутреннихЗаказов.ВнутреннийЗаказ КАК Документ.ВнутреннийЗаказ) КАК ВнутреннийЗаказ
		|ПОМЕСТИТЬ ВТ_СписокНомеров
		|ИЗ
		|	&СписокВнутреннихЗаказов КАК СписокВнутреннихЗаказов
		|ГДЕ
		|	СписокВнутреннихЗаказов.ВнутреннийЗаказ В(&Массив)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕстьNULL(ВТ_СписокНомеров.ВнутреннийЗаказ.Номер, ""Св. запас"") КАК Номер
		|ИЗ
		|	ВТ_СписокНомеров КАК ВТ_СписокНомеров
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номер";
	
	Запрос.УстановитьПараметр("Массив", МассивВнутреннихЗаказов);
	Запрос.УстановитьПараметр("СписокВнутреннихЗаказов", СписокВнутреннихЗаказов);	
	
	Список = Запрос.Выполнить().Выгрузить();
		
	СписокВнутреннихЗаказов = "";
	НуженРазделитель = Ложь;
	
	Для Каждого ВнутреннийЗаказ ИЗ Список Цикл
		
		Если НуженРазделитель Тогда
			СписокВнутреннихЗаказов = СписокВнутреннихЗаказов + "; ";
		КонецЕсли;
		
		НуженРазделитель = Истина;

		СписокВнутреннихЗаказов = СписокВнутреннихЗаказов +  ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(ВнутреннийЗаказ.Номер);
		  
	КонецЦикла;
	
	Возврат СписокВнутреннихЗаказов;	
		
КонецФункции


#КонецОбласти 

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти



 
 
 


 











