#Область ОбработчикиСобытий

Процедура ЗаказСогласованПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВнутреннийЗаказ,"ЗаказСогласованПТО");
	
КонецПроцедуры

Процедура СогласоватьЗаказПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
		
	НоваяЗадача = Задачи.ЗадачиИсполнителейПТО.СоздатьЗадачу(); 
	НоваяЗадача.БизнесПроцесс = ЭтотОбъект.Ссылка;    
	НоваяЗадача.ТочкаМаршрута = ТочкаМаршрутаБизнесПроцесса; 	
	НоваяЗадача.Дата = ТекущаяДатаСеанса();  
	
	НомерИДатаВнутреннегоЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВнутреннийЗаказ,"Номер, Дата");
	НоваяЗадача.Наименование = СтрШаблон("Согласовать внутренний заказ № %1 от %2.", 
								НомерИДатаВнутреннегоЗаказа.Номер, Формат(НомерИДатаВнутреннегоЗаказа.Дата,"ДФ=dd.MM.yyyy")); 	 
	НоваяЗадача.Инициатор = КадрыСервер.ТекущийСотрудник();
	НоваяЗадача.Должность = Справочники.Должности.РуководительПТО;
	ФормируемыеЗадачи.Добавить(НоваяЗадача);
	
	СтандартнаяОбработка = Ложь;
	Записать(); 
	
КонецПроцедуры


Процедура ДоработатьЗаказПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
	
	НоваяЗадача = Задачи.ЗадачиИсполнителейПТО.СоздатьЗадачу(); 
	НоваяЗадача.БизнесПроцесс = ЭтотОбъект.Ссылка;    
	НоваяЗадача.ТочкаМаршрута = ТочкаМаршрутаБизнесПроцесса;
	НоваяЗадача.Дата = ТекущаяДатаСеанса();
	НоваяЗадача.Наименование = СтрШаблон("Доработать и отправить на согласование внутренний заказ № %1 от %2.", 
										 ВнутреннийЗаказ.Номер, Формат(ВнутреннийЗаказ.Дата,"ДФ=dd.MM.yyyy")); 	 
	НоваяЗадача.Инициатор = КадрыСервер.ТекущийСотрудник();
	
	ПредыдущаяЗадача = БизнесПроцессыИЗадачиСервер.ПолучитьПоследююЗадачуПоТочкеМаршрута(ЭтотОбъект.Ссылка, БизнесПроцессы.ПередачаЗаказаВОМТС.ТочкиМаршрута.СогласоватьЗаказ);  
	ИсполнительИЗадание = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПредыдущаяЗадача,"ИсполнительБудущейЗадачи, Замечания");
	НоваяЗадача.Исполнитель = ИсполнительИЗадание.ИсполнительБудущейЗадачи;
	
	НоваяЗадача.Замечания = ИсполнительИЗадание.Замечания;
	ФормируемыеЗадачи.Добавить(НоваяЗадача);
	
	СтандартнаяОбработка = Ложь;
	Записать(); 
	 
КонецПроцедуры

Процедура ОтправитьНепосредственноВОМТСПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВнутреннийЗаказ,"ЗаказОтправленВОМТС");
	
КонецПроцедуры

Процедура ОтправитьВОМТСПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)

	НоваяЗадача = Задачи.ЗадачиИсполнителейПТО.СоздатьЗадачу(); 
	НоваяЗадача.БизнесПроцесс = ЭтотОбъект.Ссылка;    
	НоваяЗадача.ТочкаМаршрута = ТочкаМаршрутаБизнесПроцесса;
	НоваяЗадача.Дата = ТекущаяДатаСеанса();
	НоваяЗадача.Наименование = СтрШаблон("Отправить в ОМТС внутренний заказ № %1 от %2.", 
										  ВнутреннийЗаказ.Номер, Формат(ВнутреннийЗаказ.Дата,"ДФ=dd.MM.yyyy"));
	НоваяЗадача.Инициатор = КадрыСервер.ТекущийСотрудник();
	НоваяЗадача.Должность = Справочники.Должности.РуководительПТО;
	ФормируемыеЗадачи.Добавить(НоваяЗадача);
	
	СтандартнаяОбработка = Ложь;
	Записать();
	
КонецПроцедуры

Процедура СоздатьЗадачуРаспределениеЗаявкиПоИсполителямОбработка(ТочкаМаршрутаБизнесПроцесса)
	
	Запрос = Новый  Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВнутреннийЗаказТовары.Номенклатура КАК Номенклатура,
	|	ВнутреннийЗаказТовары.ЕдиницаИзмеренияЗаказа КАК ЕдиницаИзмерения,
	|	СУММА(ВнутреннийЗаказТовары.Количество) КАК Количество,
	|	ВнутреннийЗаказТовары.ДополнительныеХарактеристикиНоменклатуры КАК ДополнительныеХарактеристикиНоменклатуры
	|ИЗ
	|	Документ.ВнутреннийЗаказ.Товары КАК ВнутреннийЗаказТовары
	|ГДЕ
	|	ВнутреннийЗаказТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВнутреннийЗаказТовары.Номенклатура,
	|	ВнутреннийЗаказТовары.ЕдиницаИзмеренияЗаказа,
	|	ВнутреннийЗаказТовары.ДополнительныеХарактеристикиНоменклатуры";
	
	 Запрос.УстановитьПараметр("Ссылка", ВнутреннийЗаказ); 
	 Результат = Запрос.Выполнить();
	 
	 Если Результат.Пустой() Тогда
		 Возврат;
	 КонецЕсли;
	 
	 СписокМатериалов = Результат.Выгрузить();
	
	 БизнесПроцессыИЗадачиСервер.СоздатьЗадачуРаспределениеЗаявкиПоИсполителям(ВнутреннийЗаказ, СписокМатериалов);
	
	
КонецПроцедуры

#КонецОбласти
